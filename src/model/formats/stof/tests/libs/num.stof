//
// Copyright 2024 Formata, Inc. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//


#[test]
fn absolute_value() {
    assert_eq((-23).abs(), 23);
    assert_eq((-5.).abs(), 5.0);
}

#[test]
fn maximum() {
    let max = Num.max(0, 12, 2, 43, 32, 12);
    assert_eq(max, 43);
}

#[test]
fn max_units() {
    let max = Num.max(2g, 0.5kg, 100mg);
    assert_eq(max, 0.5kg);
}

#[test]
fn max_list() {
    let max = Num.max([34, 32, 100, 23], [24, 56, 300, 43]);
    assert_eq(max, 300);
}

#[test]
fn minimum() {
    let min = Num.min(29, -49, 23, -23);
    assert_eq(min, -49);
}

#[test]
fn min_units() {
    let min = Num.min(100mg, 43g, 1kg);
    assert_eq(min, 100mg);
}

#[test]
fn min_list() {
    let min = Num.min([20, 40, -50, 43], [-3, 23, 34, 15, -34]);
    assert_eq(min, -50);
}

#[test]
fn parse_numbers() {
    let val = '0b001' as int;
    assert_eq(val, 1);

    val = '0xff' as int;
    assert_eq(val, 255);

    val = '0o4' as int;
    assert_eq(val, 4);

    val = '12kg' as float;
    assert_eq(val, 12000g);
}

#[test]
fn num_len() {
    let len = (56).len();
    assert_eq(len, 56);
}

#[test]
fn num_at() {
    const a = 45;

    let at = a.at(5);
    assert_eq(at, 5);
    
    at = a.at(50);
    assert_eq(at, 45);
}

#[test]
fn num_iter() {
    let total = 0;
    // uses .len() & .at(idx) for iteration
    for (const i in 10) {
        if (first) { assert_eq(i, 0); continue; }
        if (last) { assert_eq(i, 9); continue; }

        total += 1;
    }
    assert_eq(total, 8);
}
